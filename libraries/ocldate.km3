package ocldate { 

class OclDate
{ stereotype external; 
  stereotype component; 

  static attribute systemTime : long; 
  
  attribute time : long; 
  attribute year : int; 
  attribute month : int; 
  attribute day : int; 
  attribute weekday : int; 
  attribute hour : int; 
  attribute minute : int; 
  attribute second : int; 

  static operation newOclDate() : OclDate
  pre: true
  post: OclDate->exists( d | d.time = OclDate.systemTime & result = d ); 

  static operation newOclDate_String(s : String) : OclDate
  pre: true post: true 
  activity: var items : Sequence(String) ; items := s->allMatches("[0-9]+"); var d : OclDate; d := OclDate.newOclDate_Time(0) ; if items->size() >= 3 then d.year := items->at(1)->toInteger() ; d.month := items->at(2)->toInteger() ; d.day := items->at(3)->toInteger() else skip ; if items->size() >= 6 then d.hour := items->at(4)->toInteger() ; d.minute := items->at(5)->toInteger() ; d.second := items->at(6)->toInteger() else skip ; return d;

  static operation newOclDate_Time(t : long) : OclDate
  pre: true
  post: OclDate->exists( d | d.time = t & result = d ); 

  static operation newOclDate_YMD(y : int, m : int, d : int) : OclDate
  pre: true
  post: OclDate->exists( dte | dte.year = y & dte.month = m & dte.day = d & result = dte ); 

  static operation newOclDate_YMDHMS(y : int, m : int, d : int, h : int, mn : int, s : int) : OclDate
  pre: true
  post: OclDate->exists( dte | dte.year = y & dte.month = m & dte.day = d & dte.hour = h & dte.minute = mn & dte.second = s & dte.result = dte ); 

  operation setTime(t : long) : void
  pre: true  
  post: time = t; 

  operation getTime() : long
  pre: true  
  post: result = time; 

  operation dateBefore(d : OclDate) : boolean
  pre: true 
  post: if time < d.time then result = true else result = false endif;

  operation dateAfter(d : OclDate) : boolean
  pre: true
  post: 
     if time > d.time then result = true else result = false endif; 

  operation compareToYMD(d : OclDate) : int
  pre: true
  post: 
    (year /= d.year => result = (year - d.year)*365) & 
    (year = d.year & month /= d.month => result = (month - d.month)*30) & 
    (year = d.year & month = d.month => result = (day - d.day));  

  static operation maxDateYMD(d1 : OclDate, d2 : OclDate) : OclDate
  pre: true
  post: 
    (0 < d1.compareTo(d2) => result = d2) & 
    (true => result = d1); 

  operation yearDifference(d : OclDate) : long
  pre: true 
  post: result = (time - d.time)/(31536000000); 

  operation monthDifference(d : OclDate) : long
  pre: true 
  post: result = (time - d.time)/(2628000000); 

  operation dayDifference(d : OclDate) : long
  pre: true 
  post: result = (time - d.time)/(86400000); 

  operation hourDifference(d : OclDate) : long
  pre: true 
  post: result = (time - d.time)/(3600000);

  operation minuteDifference(d : OclDate) : long
  pre: true 
  post: result = (time - d.time)/(60000);
 
  operation secondDifference(d : OclDate) : long
  pre: true 
  post: result = (time - d.time)/(1000);

  static operation getSystemTime() : long
  pre: true
  post: result = OclDate.systemTime;

  static operation setSystemTime(t : long)
  pre: true
  post: OclDate.systemTime = t;

  operation getYear() : int
  pre: true 
  post: result = year; 

  operation getMonth() : int
  pre: true 
  post: result = month; 

  operation getDate() : int
  pre: true post: result = day; 

  operation getDay() : int
  pre: true post: result = weekday;

  operation getHour() : int
  pre: true post: result = hour;
 
  operation getHours() : int
  pre: true post: result = hour;

  operation getMinute() : int
  pre: true post: result = minute;

  operation getMinutes() : int
  pre: true post: result = minute;

  operation getSecond() : int
  pre: true post: result = second;

  operation getSeconds() : int
  pre: true post: result = second;

  operation addYears(y : int) : OclDate
  pre: true 
  post: OclDate->exists( d | d.time = time + y*31536000000 & result = d); 

  operation addMonths(m : int) : OclDate
  pre: true 
  post: OclDate->exists( d | d.time = time + m*2628000000 & result = d); 

  operation addMonthYMD(m : int) : OclDate
  pre: true 
  post: 
    (month + m > 12 => 
       result = OclDate.newOclDate_YMD(year + 1, (month + m) mod 12, day)) & 
    (month + m <= 12 => 
       result = OclDate.newOclDate_YMD(year, month + m, day)); 

  operation subtractMonthYMD(m : int) : OclDate
  pre: true 
  post: 
    (month - m <= 0 => 
       result = OclDate.newOclDate_YMD(year - 1, 12 - (m - month), day)) & 
    (month + m <= 12 => 
       result = OclDate.newOclDate_YMD(year, month - m, day)); 

  operation addDays(d : int) : OclDate
  pre: true 
  post: OclDate->exists( dt | dt.time = time + d*86400000 & result = dt); 

  operation addHours(h : int) : OclDate
  pre: true 
  post: OclDate->exists( dt | dt.time = time + h*3600000 & result = dt); 

  operation addMinutes(n : int) : OclDate
  pre: true 
  post: OclDate->exists( dt | dt.time = time + n*60000 & result = dt); 

  operation addSeconds(s : int) : OclDate
  pre: true 
  post: OclDate->exists( dt | dt.time = time + s*1000 & result = dt); 

  operation toString() : String
  pre: true 
  post: result = day + "-" + month + "-" + year + " " + hour + ":" + minute + ":" + second; 

  static query leapYear(yr : int) : boolean
  pre: true
  post: 
    (yr mod 4 = 0 & yr mod 100 /= 0 => result = true) &
    (yr mod 400 = 0 => result = true) & 
    (true => result = false); 


  operation isLeapYear() : boolean
  pre: true 
  post: result = OclDate.leapYear(year); 

  static query monthDays(mn : int, yr : int) : int
  pre: true
  post: 
    (mn : Set{4,6,9,11} => result = 30) & 
    (mn = 2 & OclDate.leapYear(yr) => result = 29) & 
    (mn = 2 & not(OclDate.leapYear(yr)) => result = 28) & 
    (true => result = 31); 


  operation daysInMonth() : int
  pre: true
  post: result = OclDate.monthDays(month, year); 

  operation isEndOfMonth() : boolean
  pre: true
  post: 
    (day = OclDate.monthDays(month,year) => result = true) & 
    (true => result = false);  

  static operation daysBetweenDates(d1 : OclDate, d2 : OclDate) : int
  pre: true 
  post: true
  activity: 
    var startDay : int := d1.day; 
    var startMonth : int := d1.month;
    var startYear : int := d1.year;

    var endDay : int := d2.day; 
    var endMonth : int := d2.month;
    var endYear : int := d2.year;

    var days : int := 0;

    while startYear < endYear or (startYear = endYear & startMonth < endMonth)
    do (var daysinmonth : int := 
          OclDate.monthDays(startMonth,startYear);   

        days := days + daysinmonth - startDay + 1; 
        startDay := 1;
        startMonth := startMonth + 1; 

        if startMonth > 12
        then
            startMonth := 1;
            startYear := startYear + 1
        else 
          skip
     ); 
    days := days + endDay - startDay; 
    return days; 


 }


}


